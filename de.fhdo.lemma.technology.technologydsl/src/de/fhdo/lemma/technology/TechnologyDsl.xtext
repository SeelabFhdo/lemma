/**
 * @author <a href="mailto:florian.rademacher@fh-dortmund.de">Florian Rademacher</a>
 */
grammar de.fhdo.lemma.technology.TechnologyDsl with de.fhdo.lemma.data.DataDsl

import "de.fhdo.lemma.technology"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "de.fhdo.lemma.data" as data

Technology returns Technology:
    imports+=TechnologyImport*
    'technology' name=ID '{'
        ('types' '{'
            (primitiveTypes+=TechnologySpecificPrimitiveType |
                collectionTypes+=TechnologySpecificCollectionType |
                dataStructures+=TechnologySpecificDataStructure
            )+

            ('compatibility' 'matrix' '{'
                compatibilityEntries+=CompatibilityMatrixEntry+
            '}')?
        '}')?

        ('protocols' '{'
            protocols+=Protocol+
        '}')?

        ('service' 'aspects' '{'
            serviceAspects+=ServiceAspect+
        '}')?

        ('deployment' 'technologies' '{'
            deploymentTechnologies+=DeploymentTechnology+
        '}')?

        ('infrastructure' 'technologies' '{'
            infrastructureTechnologies+=InfrastructureTechnology+
        '}')?

        ('operation' 'aspects' '{'
            operationAspects+=OperationAspect+
        '}')?
    '}'
;

TechnologyImport returns TechnologyImport:
    'import' 'technology' 'from' importURI=STRING 'as' name=ID
;

Protocol returns Protocol:
    communicationType=CommunicationType
    name=ID
    'data' 'formats' dataFormats+=DataFormat (',' dataFormats+=DataFormat)*
    (default?='default' 'with' 'format' defaultFormat=[DataFormat|STRING])?
    ';'
;

enum ExchangePattern returns ExchangePattern:
    IN='in' | OUT='out' | INOUT='inout'
;

enum CommunicationType returns CommunicationType:
    SYNCHRONOUS='sync' | ASYNCHRONOUS='async'
;

DataFormat returns DataFormat:
    formatName=STRING
;

TechnologySpecificPrimitiveType returns TechnologySpecificPrimitiveType:
    'primitive' 'type' name=ID
    (
        'based' 'on' basicBuiltinPrimitiveTypes+=PrimitiveType
            (',' basicBuiltinPrimitiveTypes+=PrimitiveType)*
        (default?='default')?
    )? ';'
;

TechnologySpecificCollectionType returns TechnologySpecificCollectionType:
    'collection' 'type' name=ID ';'
;

TechnologySpecificDataStructure returns TechnologySpecificDataStructure:
    'structure' 'type' name=ID ';'
;

PossiblyImportedTechnologySpecificType returns PossiblyImportedTechnologySpecificType:
    (^import=[TechnologyImport] '::')? type=[data::Type|QualifiedName]
;

enum CompatibilityDirection returns CompatibilityDirection:
    MAPPING_TO_COMPATIBLE_TYPES='<-' |
    COMPATIBLE_TYPES_TO_MAPPING='->' |
    BIDIRECTIONAL='<->'
;

CompatibilityMatrixEntry returns CompatibilityMatrixEntry:
    compatibleTypes+=PossiblyImportedTechnologySpecificType
    (',' compatibleTypes+=PossiblyImportedTechnologySpecificType)*
    direction=CompatibilityDirection
    mappingType=PossiblyImportedTechnologySpecificType
    ';'
;

DeploymentTechnology returns DeploymentTechnology:
    name=ID '{'
        'operation' 'environments' '=' operationEnvironments+=OperationEnvironment
            (',' operationEnvironments+=OperationEnvironment)*
            ';'

        ('service' 'properties' '{'
            serviceProperties+=TechnologySpecificProperty+
        '}')?
    '}'
;

InfrastructureTechnology returns InfrastructureTechnology:
    name=ID '{'
        'operation' 'environments' '=' operationEnvironments+=OperationEnvironment
            (',' operationEnvironments+=OperationEnvironment)*
            ';'

        ('service' 'properties' '{'
            serviceProperties+=TechnologySpecificProperty+
        '}')?
    '}'
;

OperationEnvironment returns OperationEnvironment:
    environmentName=STRING (default?='default')?
;

enum PropertyFeature returns PropertyFeature:
    MANDATORY = 'mandatory' |
    SINGLE_VALUED = 'singleval'
;

TechnologySpecificProperty returns TechnologySpecificProperty:
    type=PrimitiveType name=ID
    ('=' defaultValue=PrimitiveValue)?
    ('<'
        features+=PropertyFeature (',' features+=PropertyFeature)*
    '>')?
    ';'
;

enum ServiceJoinPointType returns JoinPointType:
    COMPLEX_TYPES = 'types' |
    DATA_FIELDS = 'fields' |
    DATA_OPERATIONS = 'domainOperations' |
    DATA_OPERATION_PARAMETERS = 'domainParameters' |
    MICROSERVICES = 'microservices' |
    INTERFACES = 'interfaces' |
    OPERATIONS = 'operations' |
    PARAMETERS = 'parameters'
;

ServiceAspectPointcut returns ServiceAspectPointcut:
    forExchangePattern?='exchange_pattern' '=' exchangePattern=ExchangePattern |
    forCommunicationType?='communication_type' '=' communicationType=CommunicationType |
    forProtocol?='protocol' '=' protocol=[Protocol] |
    forDataFormat?='data_format' '=' dataFormat=[DataFormat|STRING]
;

ServiceAspectPointcutSelector returns ServiceAspectPointcutSelector:
    'selector' '('
        pointcuts+=ServiceAspectPointcut (',' pointcuts+=ServiceAspectPointcut)*
    ')' ';'
;

enum AspectFeature returns AspectFeature:
    SINGLE_VALUED = 'singleval'
;

ServiceAspect returns ServiceAspect:
    'aspect' name=ID
    ('<'
        features+=AspectFeature (',' features+=AspectFeature)*
    '>')?
    'for' joinPoints+=ServiceJoinPointType (',' joinPoints+=ServiceJoinPointType)*
    (
        ('{'
            pointcutSelectors+=ServiceAspectPointcutSelector*
            properties+=TechnologySpecificProperty*
        '}')
        |
        ';'
    )
;

enum OperationJoinPointType returns JoinPointType:
    CONTAINERS = 'containers' | INFRASTRUCTURE_NODES = 'infrastructure'
;

OperationAspectPointcut returns OperationAspectPointcut:
    forTechnology?='technology' '=' technology=[ecore::EObject|ID]
;

OperationAspectPointcutSelector returns OperationAspectPointcutSelector:
    'selector' '('
        pointcuts+=OperationAspectPointcut (',' pointcuts+=OperationAspectPointcut)*
    ')' ';'
;

OperationAspect returns OperationAspect:
    'aspect' name=ID
    ('<'
        features+=AspectFeature (',' features+=AspectFeature)*
    '>')?
    'for' joinPoints+=OperationJoinPointType (',' joinPoints+=OperationJoinPointType)*
    (
        ('{'
            pointcutSelectors+=OperationAspectPointcutSelector*
            properties+=TechnologySpecificProperty*
        '}')
        |
        ';'
    )
;