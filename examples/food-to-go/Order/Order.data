import datatypes from "../Common/Common.data" as Common

context Order {
    structure Order<aggregate, entity> {
        long id<identifier>,
        immutable long ^version,
        immutable OrderState state,
        long consumerId = -1,
        long restaurantId = -1,
        immutable OrderLineItems orderLineItems<part>,
        hide immutable DeliveryInformation deliveryInformation<part>,
        hide immutable PaymentInformation paymentInformation<part>,
        hide immutable Common::Common.Money orderMinimum<part>,
        function Common::Common.Money getOrderTotal()
    }

    enum OrderState {
        APPROVED,
        APPROVAL_PENDING,
        REJECTED,
        REVISION_PENDING
    }

    structure OrderLineItem<valueObject> {
        immutable int quantity,
        immutable string menuItemId,
        immutable string name,
        immutable Common::Common.Money price,
        function Common::Common.Money deltaForChangedQuantity(int newQuantity)<sideEffectFree>,
        function Common::Common.Money getTotal<sideEffectFree>
    }

    structure OrderLineItems<valueObject> {
        immutable OrderLineItemsType lineItems,
        function OrderLineItem findOrderLineItem(string lineItemId)<sideEffectFree>,
        function Common::Common.Money orderTotal<sideEffectFree>
    }

    list OrderLineItemsType { OrderLineItem i }

    structure DeliveryInformation<valueObject> {
        immutable date deliveryTime,
        immutable Address deliveryAddress
    }

    structure Address<valueObject> {
        immutable string street1,
        immutable string street2,
        immutable string city,
        immutable string state,
        immutable string zip
    }

    structure PaymentInformation<valueObject> {
        immutable string paymentToken
    }

    structure OrderService<service> {
        function Order createOrder(long consumerId, long restaurantId,
            MenuItemIdAndQuantityList lineItems),
        function Order updateOrder(long orderId, Order order),
        function Order cancel(long orderId),
        procedure approveOrder(long orderId),
        procedure rejectOrder(long orderId)
    }

    structure MenuItemIdAndQuantity {
        string menuItemId,
        immutable int quantity
    }

    list MenuItemIdAndQuantityList { MenuItemIdAndQuantity itemQuantity }
}
