module de.fhdo.lemma.GenerateServiceDsl

import org.eclipse.xtext.xtext.generator.*
import org.eclipse.xtext.xtext.generator.model.project.*

var rootPath = ".."

Workflow {
    component = XtextGenerator {
        configuration = {
            project = StandardProjectConfig {
                baseName = "de.fhdo.lemma.servicedsl"
                rootPath = rootPath
                runtimeTest = {
                    enabled = true
                }
                eclipsePlugin = {
                    enabled = true
                }
                eclipsePluginTest = {
                    enabled = true
                }
                createEclipseMetaData = true
            }
            code = {
                encoding = "UTF-8"
                lineDelimiter = "\n"
                fileHeader = "/*\n * generated by Xtext \${version}\n */"
            }
        }
        language = StandardLanguage {
            name = "de.fhdo.lemma.ServiceDsl"
            fileExtensions = "services"
            referencedResource = "platform:/resource/de.fhdo.lemma.servicedsl.metamodel/model/ServiceViewpointModel.xcore"
            referencedResource = "platform:/resource/de.fhdo.lemma.data.datadsl.metamodel/model/DataViewpointModel.xcore"
            referencedResource = "platform:/resource/de.fhdo.lemma.technology.technologydsl.metamodel/model/TechnologyDefinitionModel.xcore"

            fragment = ecore2xtext.Ecore2XtextValueConverterServiceFragment2 auto-inject {}

            formatter = {
                generateStub = true
            }

            serializer = {
                generateStub = false
            }
            validator = {
                // composedCheck = "org.eclipse.xtext.validation.NamesAreUniqueValidator"
            }
        }
    }
}
