/**
 * @author <a href="mailto:florian.rademacher@fh-dortmund.de">Florian Rademacher</a>
 */

grammar de.fhdo.lemma.data.DataDsl with org.eclipse.xtext.common.Terminals

import "de.fhdo.lemma.data"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

DataModel returns DataModel:
    complexTypeImports+=ComplexTypeImport*
    (versions+=Version+ | contexts+=Context+ | complexTypes+=ComplexType+)
;

ComplexTypeImport returns ComplexTypeImport:
    'import' 'datatypes' 'from' importURI=STRING 'as' name=ID
;

Version returns Version:
    'version' name=ID '{'
         (complexTypes+=ComplexType+ | contexts+=Context+)?
     '}'
;

Context returns Context:
    'context' name=ID '{'
        complexTypes+=ComplexType+
    '}'
;

ComplexType returns ComplexType:
    DataStructure | CollectionType | Enumeration
;

enum ComplexTypeFeature returns ComplexTypeFeature:
    AGGREGATE = 'aggregate' |
    APPLICATION_SERVICE = 'applicationService' |
    DOMAIN_EVENT = 'domainEvent' |
    DOMAIN_SERVICE = 'domainService' |
    ENTITY = 'entity' |
    FACTORY = 'factory' |
    INFRASTRUCTURE_SERVICE = 'infrastructureService' |
    REPOSITORY = 'repository' |
    SERVICE = 'service' |
    SPECIFICATION = 'specification' |
    VALUE_OBJECT = 'valueObject'
;

DataStructure returns DataStructure:
    'structure' name=ID
    ('<' features+=ComplexTypeFeature (',' features+=ComplexTypeFeature)* '>')?
    ('extends' super=[DataStructure|QualifiedName])? '{'
        (
            (dataFields+=DataField | operations+=DataOperation)
            (',' (dataFields+=DataField | operations+=DataOperation))*
        )?
    '}'
;

CollectionType returns CollectionType:
    'collection' name=ID '{'
        dataFields+=DataField (',' dataFields+=DataField)*
    '}'
    |
    'collection' name=ID '{'
        primitiveType=PrimitiveType
    '}'
;

enum DataFieldFeature returns DataFieldFeature:
    IDENTIFIER = 'identifier' |
    NEVER_EMPTY = 'neverEmpty' |
    PART = 'part'
;

DataField returns DataField:
    ^hidden?='hide'?
    immutable?='immutable'?
    (
        primitiveType=PrimitiveType |
        complexType=[ComplexType|QualifiedName] |
        importedComplexType=ImportedComplexType
    )?
    name=ID
    ('=' initializationValue=PrimitiveValue)?
    ('<' features+=DataFieldFeature (',' features+=DataFieldFeature)* '>')?
;

Enumeration returns Enumeration:
    'enum' name=ID
    ('<' features+=ComplexTypeFeature (',' features+=ComplexTypeFeature)* '>')? '{'
        fields+=EnumerationField (',' fields+=EnumerationField)*
    '}'
;

EnumerationField returns EnumerationField:
    name=ID ('(' initializationValue=PrimitiveValue ')')?
;

enum DataOperationFeature returns DataOperationFeature:
    CLOSURE = 'closure' |
    IDENTIFIER = 'identifier' |
    SIDE_EFFECT_FREE = 'sideEffectFree' |
    VALIDATOR = 'validator'
;

DataOperation returns DataOperation:
    ^hidden?='hide'?
    (
        hasNoReturnType ?= 'procedure' |
        'function'
            (
                primitiveReturnType=PrimitiveType |
                complexReturnType=[ComplexType|QualifiedName] |
                importedComplexReturnType=ImportedComplexType
            )?
    )
    name=ID
    ('(' parameters+=DataOperationParameter? (',' parameters+=DataOperationParameter)* ')')?
    ('<' features+=DataOperationFeature (',' features+=DataOperationFeature)* '>')?
;

DataOperationParameter returns DataOperationParameter:
    (
        primitiveType=PrimitiveType |
        complexType=[ComplexType|QualifiedName] |
        importedComplexType=ImportedComplexType
    )
    name=ID
;

PrimitiveValue returns PrimitiveValue:
    numericValue=BIG_DECIMAL | booleanValue=BOOLEAN | stringValue=STRING
;

ImportedComplexType returns ImportedComplexType:
    ^import=[ComplexTypeImport] '::' importedType=[Type|QualifiedName]
;

PrimitiveType returns PrimitiveType:
    {PrimitiveBoolean} 'boolean' |
    {PrimitiveByte} 'byte' |
    {PrimitiveCharacter} 'char' |
    {PrimitiveDate} 'date' |
    {PrimitiveDouble} 'double' |
    {PrimitiveFloat} 'float' |
    {PrimitiveInteger} 'int' |
    {PrimitiveLong} 'long' |
    {PrimitiveShort} 'short' |
    {PrimitiveString} 'string' |
    {PrimitiveUnspecified} 'unspecified'
;

QualifiedName:
    ID ('.' ID)*
;

terminal BIG_DECIMAL returns ecore::EBigDecimal: '-'?INT ('.' INT)?;
terminal BOOLEAN returns ecore::EBoolean: 'true' | 'false';